plugins {
    id 'com.android.application'
}

def getGitCommitCount() {
    def isShallowRepo = providers.exec {
        commandLine 'git', 'rev-parse', '--is-shallow-repository'
    }.standardOutput.asText.get().trim().equalsIgnoreCase('true')

    if (isShallowRepo) {
        throw new GradleException("Cannot derive versionCode from shallow Git history. Fetch full history (e.g. checkout with fetch-depth: 0).")
    }

    return providers.exec {
        commandLine 'git', 'rev-list', '--count', 'HEAD'
    }.standardOutput.asText.get().trim().toInteger()
}

def gitCommitCount = getGitCommitCount()

dependencies {
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'

    testImplementation 'junit:junit:4.13.2'
}

android {
    namespace 'com.example.smsalarm'
    compileSdk = 34

    defaultConfig {
        applicationId "com.example.smsalarm"
        minSdk = 29
        targetSdk = 34
        versionCode gitCommitCount
        versionName "1.0"
    }

    // sign config
    signingConfigs {
        release {
            storeFile file("../smsalarm.keystore")
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            // use sign
            signingConfig signingConfigs.release

            minifyEnabled false
            proguardFiles getDefaultProguardFile(
                    'proguard-android-optimize.txt'),
                    'proguard-rules.pro'
        }
    }

    buildFeatures {
        compose false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
}

tasks.register("incrementVersionCode") {
    group = "versioning"
    description = "Displays the APK versionCode derived from git commit count"

    doLast {
        println("Current git commit count versionCode: ${gitCommitCount}")
    }
}
